version 1.1;

ns junos = "http://xml.juniper.net/junos/*/junos";
ns xnm = "http://xml.juniper.net/xnm/1.1/xnm";
ns jcs = "http://xml.juniper.net/junos/commit-scripts/1.0";

import "../import/junos.xsl";

param $use-lacp;
param $cn-name;

var $hash := {
	<cn> {
		<name> "CN1";
		<pxe> "ge-3/2/3";
		<non-pxe> "ge-3/2/0";
		<agg>	"ae0";
	}
	<cn> {
		<name> "CN2";
		<pxe> "ge-1/0/5";
		<non-pxe> "ge-1/1/5";
		<agg>	"ae1";
	}
	<cn> {
		<name> "CN3";
		<pxe> "ge-1/0/6";
		<non-pxe> "ge-1/1/6";
		<agg>	"ae2";
	}
	/* Add compute nodes here */
}

match / {
    <event-script-results> {

	var $node = $hash/cn[name == $cn-name];
	var $pxe = $node/pxe;
	var $non-pxe = $node/non-pxe;
	var $agg = $node/agg;
	
	if ( $use-lacp == "yes" ) { 
       		var $configuration-change = <configuration> {
	    		<interfaces> {
   					<interface> {
						<name> $pxe;
        					<gigether-options> {
            						<ieee-802.3ad> {
								<bundle> $agg;
							}
        					}
    					}
   					<interface> {
						<name> $non-pxe;
        					<gigether-options> {
            						<ieee-802.3ad> {
								<bundle> $agg;
							}
        					}
    					}
   					<interface> {
						<name> $agg;
						<aggregated-ether-options> {
							<lacp> {
								<active>;
							}
						}
    					}
	    			}
    			}
    			var $connection = jcs:open();
     			var $results := { call jcs:load-configuration( $connection, $configuration = $configuration-change ); }

    			if ($results//xnm:error) {
        			expr jcs:syslog("external.info", "Configuration change failure!!");
    			}  else {
         			expr jcs:syslog("external.info", "Configuration change success!!");
    			}
    		} else { 
			var $interface-rpc = {
				<get-interface-information> {
					<terse>;
				}
			}
			var $interfaces = jcs:invoke( $interface-rpc );
			/* var $up-count = count( $interfaces/physical-interface[name == $pxe || name == $non-pxe ]/logical-interface[oper-status == "up"]); */
			var $up-count = count( $interfaces/physical-interface[name == $pxe || name == $non-pxe ] );
        		var $configuration-change = <configuration> {
	    			<interfaces> {
   					<interface> {
						<name> $pxe;
        					<gigether-options> {
            						<ieee-802.3ad> {
								<bundle> $agg;
							}
        					}
    					}
   					<interface delete="interface"> {
						<name> $non-pxe;
					}
   					<interface> {
						<name> $agg;
						<aggregated-ether-options delete="aggregated-ether-options">;
    					}
	    			}
    			}
			if ( $up-count >= 1 ) {
    			var $connection = jcs:open();
     			var $results := { call jcs:load-configuration( $connection, $configuration = $configuration-change ); }

    			if ($results//xnm:error) {
        			expr jcs:syslog("external.info", "Configuration change failure!!");
    			}  else {
         			expr jcs:syslog("external.info", "Configuration change success!!");
    			}
			} else {
        			expr jcs:syslog("external.info", "LAG slave interfaces not UP yet. Will skip.!!!");
			}
    		}
    }
}

